# -*- mode: ruby -*-

#include lib/std.rcl
#include lib/types.rcl
#include lib/json.rcl

# --------------------------------

def mklist2(g_, n0_, n1_)
  var list_ = List_new(g_);

  List_add(g_, list_, n0_);
  List_add(g_, list_, n1_);

  return list_;
end

def mklist3(g_, n0_, n1_, n2_)
  var list_ = mklist2(g_, n0_, n1_);

  List_add(g_, list_, n2_);

  return list_;
end

# --------------------------------

def get_line_size(input_, pos_from)
  var pos_to;
  var lf_pos = char_index(input_, C_LF(), pos_from);

  if (lf_pos != -1)
    pos_to = lf_pos + 1;
  else
    # LF not found
    pos_to = char_index(input_, 0, pos_from);
  end

  return __sub(pos_to, pos_from);
end

def set_token(g_, ti, t_)
  if (__le(NUM_TOKENS_MAX(), ti))
    p_i(NUM_TOKENS_MAX());
    p_i(ti);
    panic(27); # set_token: capacity over
  end
  aset(g_ + GO_TOKENS(), ti, t_);
end

def read_token(g_, input_, pos, size)
  var line_size = 80; var [80]line;
  var sep_i;
  var [6]kind_str;
  var value_str_size = 64; var [64]value_str;
  var t_;

  substr_with_check(&line, input_, pos, pos + size, line_size);
  chomp(&line);

  sep_i = char_index(&line, 58, 0); # :
  substr(&kind_str, &line, 0, sep_i);
  substr_with_check(&value_str, &line, sep_i + 1, str_size(&line), value_str_size);

  t_ = Token_new(g_, &kind_str, &value_str);
  return t_;
end

def read_tokens(g_)
  var size;
  var pos = 0;
  var t_;
  var ti;
  var input_size_max = 150000; var [150000]input;
  check_heap_stack_overlap(g_);

  read_stdin_all(&input, input_size_max);

  while (true)
    size = get_line_size(&input, pos);
    if (size == 0)
      break;
    end

    t_ = read_token(g_, &input, pos, size);
    set_token(g_, ti, t_);
    ti = ti + 1;

    pos = pos + size;
  end

  set_num_tokens(g_, ti); # ti == number of tokens
end

def set_num_tokens(g_, num_tokens)
  *(g_ + GO_NUM_TOKENS()) = num_tokens;
end

def get_num_tokens(g_)
  return *(g_ + GO_NUM_TOKENS());
end

def is_end(g_)
  var num_tokens = get_num_tokens(g_);
  var ti = get_token_index(g_);

  return __le(num_tokens, ti);
end

# --------------------------------

def set_token_index(g_, ti)
  *(g_ + GO_TOKEN_INDEX()) = ti;
end

def get_token_index(g_)
  return *(g_ + GO_TOKEN_INDEX());
end

def inc_token_index(g_)
  set_token_index(
    g_,
    get_token_index(g_) + 1
  );
end

def peek(g_, i)
  var ti = get_token_index(g_);
  return *(g_ + GO_TOKENS() + ti + i);
end

def consume_kw(g_, str_)
  var t_ = peek(g_, 0);

  if (__and(
        Token_kind_eq(t_, TOKEN_KIND__KW()),
        Token_val_eq(t_, str_)
  ))
    inc_token_index(g_);
  else
    print_i_e(TOKEN_KIND__KW());
    putchar_e(47); # /
    print_s_e(str_);
    putchar_e(47); # /
    putchar_e(C_LF());

    print_i_e(Token_get_kind(t_));
    putchar_e(47); # /
    print_s_e(Token_get_val(t_));
    putchar_e(47); # /
    putchar_e(C_LF());

    panic(98); # consume_kw
  end
end

def consume_sym(g_, str_)
  var t_ = peek(g_, 0);
  if (__and(
        Token_kind_eq(t_, TOKEN_KIND__SYM()),
        Token_val_eq(t_, str_)
  ))
    inc_token_index(g_);
  else
    p_s(str_); # expected
    p_i(Token_get_kind(t_));
    p_s(Token_get_val(t_)); # actual
    panic(117); # consume_sym: unexpected token
  end
end

# --------------------------------

def consume_sym_one_char(g_, c)
  var [2]str;

  aset(&str, 0, c);
  aset(&str, 1, 0);

  consume_sym(g_, &str);
end

def consume_sym_paren_l(g_)
  consume_sym_one_char(g_, 40); # (
end

def consume_sym_paren_r(g_)
  consume_sym_one_char(g_, 41); # )
end

def consume_sym_cbracket_l(g_)
  consume_sym_one_char(g_, 123); # {
end

def consume_sym_cbracket_r(g_)
  consume_sym_one_char(g_, 125); # }
end

def consume_sym_semicolon(g_)
  consume_sym_one_char(g_, 59); # ;
end

def consume_sym_equal(g_)
  consume_sym_one_char(g_, 61); # =
end

# --------------------------------

def StrLitList_init(g_)
  *(g_ + GO_STR_LIT_LIST()) = List_new(g_);
end

def StrLitList_get_self(g_)
  return *(g_ + GO_STR_LIT_LIST());
end

def StrLitList_add(g_, str_, offset)
  var s_o_ = List_new(g_);
  List_add_str(g_, s_o_, str_);
  List_add_int(g_, s_o_, offset);

  List_add_list(g_, StrLitList_get_self(g_), s_o_);
end

def StrLitList_get_str(g_, i)
  var self_ = StrLitList_get_self(g_);
  var s_o_ = List_get_as_list(self_, i);

  return List_get_as_str(s_o_, 0);
end

def StrLitList_get_offset(g_, i)
  var self_ = StrLitList_get_self(g_);
  var s_o_ = List_get_as_list(self_, i);

  return List_get_as_int(s_o_, 1);
end

def StrLitList_index(g_, str_)
  var self_ = StrLitList_get_self(g_);
  var s_temp_;
  var i;

  i = 0;
  while (i < List_size(self_))
    s_temp_ = StrLitList_get_str(g_, i);
    if (str_eq(s_temp_, str_))
      return i;
    end
    i = i + 1;
  end

  return -1;
end

def StrLitList_get_new_offset(g_)
  var self_ = StrLitList_get_self(g_);
  var last_str_;
  var last_offset;
  var size;

  size = List_size(self_);
  if (size == 0)
    return 0;
  end

  var last_i = __sub(size, 1);
  last_str_   = StrLitList_get_str(   g_, last_i);
  last_offset = StrLitList_get_offset(g_, last_i);
  return last_offset + str_size(last_str_) + 1;
end

# --------------------------------

def _parse_arg(g_)
  var n;
  var t_ = peek(g_, 0);
  inc_token_index(g_);

  case
  when (Token_kind_eq(t_, TOKEN_KIND__IDENT()))
    return Node_new_str(g_, Token_get_val(t_));
  else
    panic(202); # _parse_arg: unsupported
  end
end

def parse_args(g_)
  var args_;
  var t_;
  var arg_node_;
  var [2]s_comma;

  aset(&s_comma, 0, C_COMMA());
  aset(&s_comma, 1,  0);

  args_ = List_new(g_);

  t_ = peek(g_, 0);

  if (char_at(Token_get_val(t_), 0) == 41) # )
    return args_;
  end

  arg_node_ = _parse_arg(g_);
  List_add(g_, args_, arg_node_);

  while (true)
    t_ = peek(g_, 0);

    if (char_at(Token_get_val(t_), 0) != C_COMMA())
      break;
    end

    consume_sym(g_, &s_comma);
    arg_node_ = _parse_arg(g_);
    List_add(g_, args_, arg_node_);
  end

  return args_;
end

def parse_exprs(g_)
  var exprs_;
  var t_;
  var expr_node_;
  var [2]s_comma;

  aset(&s_comma, 0, C_COMMA());
  aset(&s_comma, 1,  0);

  exprs_ = List_new(g_);

  t_ = peek(g_, 0);

  if (char_at(Token_get_val(t_), 0) == 41) # )
    return exprs_;
  end

  expr_node_ = parse_expr(g_);
  List_add(g_, exprs_, expr_node_);

  while (true)
    t_ = peek(g_, 0);

    if (char_at(Token_get_val(t_), 0) != C_COMMA())
      break;
    end

    consume_sym(g_, &s_comma);
    expr_node_ = parse_expr(g_);
    List_add(g_, exprs_, expr_node_);
  end

  return exprs_;
end

def _parse_var_declare(g_)
  var stmt_;
  var t_;
  var var_name_;
  check_heap_stack_overlap(g_);

  stmt_ = List_new(g_);

  consume_kw(g_, "var");
  List_add_str(g_, stmt_, "var");

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);
  List_add_str(g_, stmt_, var_name_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def _parse_var_init(g_)
  var stmt_;
  var t_;
  var var_name_;
  var expr_node_;
  check_heap_stack_overlap(g_);

  stmt_ = List_new(g_);

  consume_kw(g_, "var");
  List_add_str(g_, stmt_, "var");

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);
  List_add_str(g_, stmt_, var_name_);

  consume_sym_equal(g_); # =

  expr_node_ = parse_expr(g_);
  List_add(g_, stmt_, expr_node_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def _parse_var_array(g_)
  var t_;
  var width;
  var var_name_;
  var stmt_;

  consume_kw(g_, "var");

  consume_sym_one_char(g_, 91); # [

  t_ = peek(g_, 0);
  inc_token_index(g_);
  width = parse_int(Token_get_val(t_));

  consume_sym_one_char(g_, 93); # ]

  var_name_ = Token_get_val(peek(g_, 0));
  inc_token_index(g_);

  consume_sym_semicolon(g_);

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "var_array");
  List_add_str(g_, stmt_, var_name_);
  List_add_int(g_, stmt_, width);

  return stmt_;
end

def parse_var(g_)
  var [2]s_semicolon;
  var [2]s_equal;
  var t_;

  aset(&s_semicolon, 0, 59); # ;
  aset(&s_semicolon, 1,  0);

  aset(&s_equal, 0, 61); # =
  aset(&s_equal, 1,  0);

  t_ = peek(g_, 1);

  if (char_at(Token_get_val(t_), 0) == 91) # [
    _parse_var_array(g_);
  else

    t_ = peek(g_, 2);
    case
    when (Token_val_eq(t_, &s_semicolon))
      _parse_var_declare(g_);
    when (Token_val_eq(t_, &s_equal))
      _parse_var_init(g_);
    else
      print_s(Token_get_val(t_));
      panic(426); # parse_var: unexpected token
    end

  end
end

def parse_expr_addr(g_)
  var t_;
  var var_name_;
  var expr_list_;
  var expr_node_;

  consume_sym_one_char(g_, 38); # &

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var var_name_ = Token_get_val(t_);

  expr_list_ = List_new(g_);
  List_add_str(g_, expr_list_, "addr");
  List_add_str(g_, expr_list_, var_name_);

  expr_node_ = Node_new_list(g_, expr_list_);

  return expr_node_;
end

def parse_expr_deref(g_)
  var list_;
  var expr_node_;

  consume_sym_one_char(g_, 42); # *
  consume_sym_paren_l(g_);      # (
  expr_node_ = parse_expr(g_);
  consume_sym_paren_r(g_);      # )

  list_ = List_new(g_);
  List_add_str(g_, list_, "deref");
  List_add(g_, list_, expr_node_);

  return Node_new_list(g_, list_);
end

def is_binop(t_)
  var [3]s_double_equal;
  var [3]s_not_equal;

  aset(&s_double_equal, 0, 61); # =
  aset(&s_double_equal, 1, 61); # =
  aset(&s_double_equal, 2,  0);

  aset(&s_not_equal, 0, 33); # !
  aset(&s_not_equal, 1, 61); # =
  aset(&s_not_equal, 2,  0);

  case
  when (char_at(Token_get_val(t_), 0) == 43) # +
    return true;
  when (char_at(Token_get_val(t_), 0) == 42) # *
    return true;
  when (Token_val_eq(t_, &s_double_equal))
    return true;
  when (Token_val_eq(t_, &s_not_equal))
    return true;
  when (char_at(Token_get_val(t_), 0) == 60) # <
    return true;
  else
    return false;
  end
end

def _make_str_expr(g_, offset)
  # g_ + GO_STRINGS() + offset
  return Node_new_list(g_,
    mklist3(g_,
      Node_new_str(g_, "+"),
      Node_new_list(g_,
        mklist3(g_,
          Node_new_str(g_, "+"),
          Node_new_str(g_, "g_"),
          Node_new_list(g_,
            mklist2(g_,
              Node_new_str(g_, "funcall"),
              Node_new_str(g_, "GO_STRINGS")
            )
          )
        )
      ),
      Node_new_int(g_, offset)
    )
  );
end

def _parse_expr_factor_int(g_)
  var t_;
  var n;

  t_ = peek(g_, 0);
  inc_token_index(g_);

  n = parse_int(Token_get_val(t_));
  return Node_new_int(g_, n);
end

def _parse_expr_factor_ident(g_)
  var t_;
  var funcall_;
  var funcall_list_;
  var i;

  if (char_at(Token_get_val(peek(g_, 1)), 0) == 40) # (
    funcall_ = parse_funcall(g_);

    funcall_list_ = List_new(g_);
    List_add_str(g_, funcall_list_, "funcall");

    i = 0;
    while (i < List_size(funcall_))
      List_add(g_, funcall_list_, List_get(funcall_, i));
      i = i + 1;
    end

    return Node_new_list(g_, funcall_list_);
  else
    t_ = peek(g_, 0);
    inc_token_index(g_);

    return Node_new_str(g_, Token_get_val(t_));
  end
end

def _parse_expr_factor_sym(g_)
  var t_;
  var expr_node_;

  t_ = peek(g_, 0);

  case
  when (char_at(Token_get_val(t_), 0) == 40) # (
    consume_sym_paren_l(g_); # (
    expr_node_ = parse_expr(g_);
    consume_sym_paren_r(g_); # )

    return expr_node_;

  when (char_at(Token_get_val(t_), 0) == 38) # &
    return parse_expr_addr(g_);

  when (char_at(Token_get_val(t_), 0) == 42) # *
    return parse_expr_deref(g_);

  else
    panic(662); # unexpected symbol
  end
end

def _parse_expr_factor_kw(g_)
  var t_;
  var expr_node_;

  t_ = peek(g_, 0);

  if (
    __or(
      str_eq(Token_get_val(t_), "true"),
      str_eq(Token_get_val(t_), "false")
    )
  )
    expr_node_ = Node_new_str(g_, Token_get_val(t_));
    inc_token_index(g_);
    return expr_node_;
  else
    panic(563); # unexpected token
  end
end

def _parse_expr_factor_str(g_)
  var t_;
  var str_offset;
  var expr_list_;

  t_ = peek(g_, 0);
  inc_token_index(g_);

  var str_ = Token_get_val(t_);

  var i = StrLitList_index(g_, str_);

  if (__le(0, i))
    str_offset = StrLitList_get_offset(g_, i);
  else
    str_offset = StrLitList_get_new_offset(g_);
    StrLitList_add(g_, str_, str_offset);
  end

  return _make_str_expr(g_, str_offset);
end

def _parse_expr_factor(g_)
  var t_;
  var tkind;

  t_ = peek(g_, 0);
  tkind = Token_get_kind(t_);

  case
  when (tkind == TOKEN_KIND__INT())
    return _parse_expr_factor_int(g_);
  when (tkind == TOKEN_KIND__IDENT())
    return _parse_expr_factor_ident(g_);
  when (tkind == TOKEN_KIND__SYM())
    return _parse_expr_factor_sym(g_);
  when (tkind == TOKEN_KIND__KW())
    return _parse_expr_factor_kw(g_);
  when (tkind == TOKEN_KIND__STR())
    return _parse_expr_factor_str(g_);
  else
    Json_print(Token_to_plain(g_, t_));
    panic(645); # not yet impl
  end
end

def parse_expr(g_)
  var list_;
  var expr_node_;
  var factor_node_;
  var s_op_;

  expr_node_ = _parse_expr_factor(g_);

  while (is_binop(peek(g_, 0)))
    s_op_ = Token_get_val(peek(g_, 0));
    inc_token_index(g_);

    factor_node_ = _parse_expr_factor(g_);

    list_ = List_new(g_);
    List_add_str(g_, list_, s_op_);
    List_add(g_, list_, expr_node_);
    List_add(g_, list_, factor_node_);

    expr_node_ = Node_new_list(g_, list_);
  end

  return expr_node_;
end

def parse_set(g_)
  var stmt_;
  var t_;
  var lhs_node_;
  var expr_node_;

  t_ = peek(g_, 0);

  if (char_at(Token_get_val(t_), 0) == 42) # *
    lhs_node_ = parse_expr_deref(g_);
  else
    inc_token_index(g_);
    lhs_node_ = Node_new_str(g_, Token_get_val(t_));
  end

  consume_sym_equal(g_); # =

  expr_node_ = parse_expr(g_);

  consume_sym_semicolon(g_); # ;

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "set");
  List_add(g_, stmt_, lhs_node_);
  List_add(g_, stmt_, expr_node_);

  return stmt_;
end

def parse_funcall(g_)
  var t_;
  var fn_name_;
  var funcall_;
  var args_;

  t_ = peek(g_, 0);
  inc_token_index(g_);
  fn_name_ = Token_get_val(t_);

  consume_sym_paren_l(g_); # (
  args_ = parse_exprs(g_);
  consume_sym_paren_r(g_); # )

  funcall_ = List_new(g_);
  List_add_str(g_, funcall_, fn_name_);

  List_add_all(g_, funcall_, args_);

  return funcall_;
end

def parse_call(g_)
  var stmt_;
  var funcall_;

  stmt_ = List_new(g_);

  List_add_str(g_, stmt_, "call");

  funcall_ = parse_funcall(g_);
  List_add_all(g_, stmt_, funcall_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def parse_return(g_)
  var expr_;
  var stmt_;

  consume_kw(g_, "return");

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "return");

  if (str_eq(Token_get_val(peek(g_, 0)), ";"))
    consume_sym_semicolon(g_); # ;
  else
    expr_ = parse_expr(g_);
    List_add(g_, stmt_, expr_);

    consume_sym_semicolon(g_); # ;
  end

  return stmt_;
end

def parse_while(g_)
  var stmt_;
  var expr_node_;
  var stmts_;

  consume_kw(g_, "while");

  consume_sym_paren_l(g_); # (
  expr_node_ = parse_expr(g_);
  consume_sym_paren_r(g_); # )

  stmts_ = parse_stmts(g_);

  consume_kw(g_, "end");

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "while");
  List_add(g_, stmt_, expr_node_);
  List_add_list(g_, stmt_, stmts_);

  return stmt_;
end

def parse_break(g_)
  var stmt_;

  consume_kw(g_, "break");
  consume_sym_semicolon(g_); # ;

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "break");

  return stmt_;
end

def _parse_when_clause(g_)
  var when_clause_;
  var cond_expr_node_;
  var stmts_;
  var t_;

  t_ = peek(g_, 0);

  case
  when (str_eq(Token_get_val(t_), "when"))
    consume_kw(g_, "when");

    consume_sym_paren_l(g_); # (
    cond_expr_node_ = parse_expr(g_);
    consume_sym_paren_r(g_); # )
  when (str_eq(Token_get_val(t_), "else"))
    consume_kw(g_, "else");
    cond_expr_node_ = Node_new_int(g_, 1);
  else
    panic(748); # _parse_when_clause
  end

  stmts_ = parse_stmts(g_);

  when_clause_ = List_new(g_);
  List_add(g_, when_clause_, cond_expr_node_);
  List_add_all(g_, when_clause_, stmts_);

  return when_clause_;
end

def parse_case(g_)
  var stmt_;
  var when_clause_;

  consume_kw(g_, "case");

  stmt_ = List_new(g_);
  List_add_str(g_, stmt_, "case");

  while (__not(Token_val_eq(peek(g_, 0), "end")))
    when_clause_ = _parse_when_clause(g_);
    List_add_list(g_, stmt_, when_clause_);
  end

  consume_kw(g_, "end");

  return stmt_;
end

def parse_vm_comment(g_)
  var t_;
  var stmt_;

  stmt_ = List_new(g_);

  consume_kw(g_, "_cmt");
  consume_sym_paren_l(g_); # (

  t_ = peek(g_, 0);
  inc_token_index(g_);

  List_add_str(g_, stmt_, "_cmt");
  List_add_str(g_, stmt_, Token_get_val(t_));

  consume_sym_paren_r(g_); # )
  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def parse_stmt(g_)
  var t_ = peek(g_, 0);
  var val_ = Token_get_val(t_);

  case
  when (str_eq(val_, "_cmt"))
    return parse_vm_comment(g_);
  when (str_eq(val_, "return"))
    return parse_return(g_);
  when (str_eq(val_, "while"))
    return parse_while(g_);
  when (str_eq(val_, "break"))
    return parse_break(g_);
  when (str_eq(val_, "case"))
    return parse_case(g_);
  else
    case
    when (
      __and(
        Token_get_kind(peek(g_, 0)) == TOKEN_KIND__IDENT(),
        char_at(Token_get_val(peek(g_, 1)), 0) == 40 # (
      )
    )
      return parse_call(g_);
    else
      return parse_set(g_);
    end
  end
end

def parse_stmts(g_)
  var stmt_;
  var stmts_;
  var t_;

  stmts_ = List_new(g_);

  while (true)
    t_ = peek(g_, 0);

    case
    when (str_eq(Token_get_val(t_), "when"))
      break;
    when (str_eq(Token_get_val(t_), "else"))
      break;
    when (str_eq(Token_get_val(t_), "end"))
      break;
    end

    stmt_ = parse_stmt(g_);
    List_add_list(g_, stmts_, stmt_);
  end

  return stmts_;
end

def parse_func(g_)
  var func_stmt_;
  var t_;
  var stmts_;
  var stmt_;
  var args_;
  var fn_name_;
  check_heap_stack_overlap(g_);

  consume_kw(g_, "def");

  func_stmt_ = List_new(g_);

  List_add_str(g_, func_stmt_, "func");

  t_ = peek(g_, 0);
  inc_token_index(g_);
  fn_name_ = Token_get_val(t_);
  List_add_str(g_, func_stmt_, fn_name_);

  consume_sym_paren_l(g_); # (

  args_ = parse_args(g_);
  List_add_list(g_, func_stmt_, args_);

  consume_sym_paren_r(g_); # )

  stmts_ = List_new(g_);

  while (true)
    t_ = peek(g_, 0);
    if (Token_val_eq(t_, "end"))
      break;
    end

    if (str_eq(Token_get_val(t_), "var"))
      stmt_ = parse_var(g_);
    else
      stmt_ = parse_stmt(g_);
    end
    List_add_list(g_, stmts_, stmt_);
  end

  consume_kw(g_, "end");

  List_add_list(g_, func_stmt_, stmts_);

  return func_stmt_;
end

def parse_top_stmt(g_)
  var t_ = peek(g_, 0);

  if (str_eq(Token_get_val(t_), "def"))
    return parse_func(g_);
  else
    panic(770); # parse_top_stmt
  end
end

def parse_top_stmts(g_)
  var top_stmts_;
  var top_stmt_;

  top_stmts_ = List_new(g_);
  List_add_str(g_, top_stmts_, "top_stmts");

  while (__not(is_end(g_)))
    top_stmt_ = parse_top_stmt(g_);
    List_add_list(g_, top_stmts_, top_stmt_);
  end

  return top_stmts_;
end

def parse(g_)
  return parse_top_stmts(g_);
end

def make_set_byte_stmt(g_, bi, byte)
  # *(offset_ + bi) = byte
  return mklist3(g_,
    Node_new_str(g_, "set"),
    Node_new_list(g_,
      mklist2(g_,
        Node_new_str(g_, "deref"),
        Node_new_list(g_,
          mklist3(g_,
            Node_new_str(g_, "+"),
            Node_new_str(g_, "offset_"),
            Node_new_int(g_, bi)
          )
        )
      )
    ),
    Node_new_int(g_, byte)
  );
end

def make_init_strings_fn(g_)
  var fn_ = List_new(g_);
  var args_ = List_new(g_);
  var stmts_ = List_new(g_);

  List_add_str(g_, args_, "g_");

  # ----
  # var offset_ = g_ + GO_STRINGS();
  var var_offset_ =
    mklist3(g_,
      Node_new_str(g_, "var"),
      Node_new_str(g_, "offset_"),
      Node_new_list(g_,
        mklist3(g_,
          Node_new_str(g_, "+"),
          Node_new_str(g_, "g_"),
          Node_new_list(g_,
            mklist2(g_,
              Node_new_str(g_, "funcall"),
              Node_new_str(g_, "GO_STRINGS")
            )
          )
        )
      )
    );

  List_add_list(g_, stmts_, var_offset_);

  # ----

  var i = 0;
  var bi = 0;
  var str_bi;
  var str_;
  var byte;
  var str_lit_list_ = StrLitList_get_self(g_);
  while (i < List_size(str_lit_list_))
    str_ = StrLitList_get_str(g_, i);
    str_bi = 0;
    while (str_bi < str_size(str_))
      byte = char_at(str_, str_bi);
      List_add_list(g_, stmts_, make_set_byte_stmt(g_, bi, byte));
      bi = bi + 1;
      str_bi = str_bi + 1;
    end
    List_add_list(g_, stmts_, make_set_byte_stmt(g_, bi, 0));
    bi = bi + 1;
    i = i + 1;
  end

  # ----

  List_add_str(g_, fn_, "func");
  List_add_str(g_, fn_, "init_strings");
  List_add_list(g_, fn_, args_);
  List_add_list(g_, fn_, stmts_);

  return fn_;
end

def GO_STRINGS() return GO_ALLOC_CURSOR() + GS_ALLOC_CURSOR(); end
def GS_STRINGS() return 1000; end

def GO_TOKENS() return GO_STRINGS() + GS_STRINGS(); end
def GS_TOKENS() return 15000; end

def GO_TOKEN_INDEX() return GO_TOKENS() + GS_TOKENS(); end
def GS_TOKEN_INDEX() return 1; end

def GO_NUM_TOKENS() return GO_TOKEN_INDEX() + GS_TOKEN_INDEX(); end
def GS_NUM_TOKENS() return 1; end

def GO_STR_LIT_LIST() return GO_NUM_TOKENS() + GS_NUM_TOKENS(); end
def GS_STR_LIT_LIST() return 1; end

def NUM_TOKENS_MAX()
  return GS_TOKENS();
end

def main()
  var [16004]g;
  var g_ = &g;

  # init globals
  init_alloc_cursor(&g);
  init_strings(&g);

  StrLitList_init(&g);
  set_token_index(&g, 0);
  assert(0, get_token_index(&g), 183);

  read_tokens(&g);

  var ast_ = parse(&g);

  var fn_init_strings_;
  if (0 < List_size(StrLitList_get_self(g_)))
    fn_init_strings_ = make_init_strings_fn(&g);
    List_add_list(&g, ast_, fn_init_strings_);
  end

  Json_print(ast_);
end
