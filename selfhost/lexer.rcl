# -*- mode: ruby -*-

#include lib/std.rcl

# --------------------------------

def str_lit_case(g_)
  return "case";
end

def str_lit_if(g_)
  return "if";
end

def str_lit_when(g_)
  return "when";
end

# --------------------------------

def is_ident_char(c)
  case
  when (range_include(48, 57, c)) # 0..9
    return true;
  when (range_include(65, 90, c)) # A..Z
    return true;
  when (range_include(97, 122, c)) # a..z
    return true;
  when (c == 95) # _
    return true;
  else
    return false;
  end
end

def find_non_ident_char(input_, pos_from)
  var pos = pos_from;
  var c = char_at(input_, pos);

  while (is_ident_char(c))
    pos = pos + 1;
    c = char_at(input_, pos);
  end

  return pos;
end

def match_ident(input_, pos_from)
  var pos = find_non_ident_char(input_, pos_from);
  return __sub(pos, pos_from);
end

def match_symbol(input_, pos_from)
  var c0 = char_at(input_, pos_from);
  var c1 = char_at(input_, pos_from + 1);

  case
  when (__and(c0 == 61, c1 == 61)) # ==
    return 2;
  when (__and(c0 == 33, c1 == 61)) # !=
    return 2;
  when (c0 == 38) # &
    return 1;
  when (c0 == 40) # (
    return 1;
  when (c0 == 41) # )
    return 1;
  when (c0 == 42) # *
    return 1;
  when (c0 == 43) # +
    return 1;
  when (c0 == C_COMMA())
    return 1;
  when (c0 == 59) # ;
    return 1;
  when (c0 == 60) # <
    return 1;
  when (c0 == 61) # =
    return 1;
  when (c0 == 91) # [
    return 1;
  when (c0 == 93) # ]
    return 1;
  else
    return 0;
  end
end

def match_comment(input_, pos_from)
  if (char_at(input_, pos_from + 0) != 35) # #
    return 0;
  end

  return __sub(
    char_index(input_, C_LF(), pos_from),
    pos_from
  );
end

def match_int(input_, pos_from)
  var c = char_at(input_, pos_from);
  var pos;

  if (c == 45) # -
    pos = non_digit_index(input_, pos_from + 1);
  else
    pos = non_digit_index(input_, pos_from);
  end

  return __sub(pos, pos_from);
end

def match_str(input_, pos_from)
  var pos;

  if (char_at(input_, pos_from) != C_DQ())
    return 0;
  end

  pos = char_index(input_, C_DQ(), pos_from + 1);
  return __sub(pos, pos_from + 1);
end

def is_kw(g_, str_)
  case
  when (str_eq(str_, "def"  )) return true;
  when (str_eq(str_, "end"  )) return true;
  when (str_eq(str_, "var"  )) return true;
  when (str_eq(str_, "return")) return true;
  when (str_eq(str_, "while")) return true;
  when (str_eq(str_, "break")) return true;
  when (str_eq(str_, str_lit_case(g_))) return true;
  when (str_eq(str_, str_lit_when(g_))) return true;
  when (str_eq(str_, "else" )) return true;
  when (str_eq(str_, "_cmt" )) return true;
  when (str_eq(str_, "true" )) return true;
  when (str_eq(str_, "false")) return true;
  when (str_eq(str_, str_lit_if(g_))) return true;
  else
    return false;
  end
end

def print_kw(g_, str_)
  print_s("kw");
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_ident(g_, str_)
  print_s("ident");
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_symbol(g_, str_)
  print_s("sym");
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_int(g_, str_)
  print_s("int");
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_str(g_, str_)
  print_s("str");
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def lex(g_)
  var [60000]input; # INPUT_MAX
  var pos = 0;
  var size;
  var s_temp_size = 40; var [40]s_temp;
  var c;

  read_stdin_all(&input, INPUT_MAX());

  while (true)
    c = char_at(&input, pos);

    if (__or(
          c == 0,
          __le(INPUT_MAX(), pos)
        ))
      break;
    end

    case
    when (__or(c == C_SPC(), c == C_LF()))
      pos = pos + 1;

    when (0 < match_comment(&input, pos))
      size = match_comment(&input, pos);
      pos = pos + size;

    when (0 < match_symbol(&input, pos))
      size = match_symbol(&input, pos);
      substr(&s_temp, &input, pos, pos + size);
      print_symbol(g_, &s_temp);
      pos = pos + size;

    when (0 < match_str(&input, pos))
      size = match_str(&input, pos);

      substr_with_check(
        &s_temp, &input, pos + 1, pos + 1 + size,
        s_temp_size
      );

      print_str(g_, &s_temp);
      pos = pos + size + 2;

    when (0 < match_int(&input, pos))
      size = match_int(&input, pos);
      substr(&s_temp, &input, pos, pos + size);
      print_int(g_, &s_temp);
      pos = pos + size;

    when (0 < match_ident(&input, pos))
      size = match_ident(&input, pos);
      substr_with_check(&s_temp, &input, pos, pos + size, s_temp_size);

      if (is_kw(g_, &s_temp))
        if (str_eq(&s_temp, str_lit_if(g_)))
          print_kw(g_, str_lit_case(g_));
          print_kw(g_, str_lit_when(g_));
        else
          print_kw(g_, &s_temp);
        end
      else
        print_ident(g_, &s_temp);
      end

      pos = pos + size;

    else
      p_i(pos);
      p_c(c);
      panic(239); # lex: unexpected pattern
    end
  end
end

# --------------------------------

def GO_STRINGS() return GO_ALLOC_CURSOR() + GS_ALLOC_CURSOR(); end
def GS_STRINGS() return 1000; end

def INPUT_MAX() return 60000; end

def main()
  var [1001]g;
  var g_ = &g;

  # init globals
  init_alloc_cursor(&g);
  init_strings(&g);

  lex(g_);
end
