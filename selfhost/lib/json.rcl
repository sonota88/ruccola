# -*- mode: ruby -*-

def Json_print_indent(lv)
  var i = lv;
  while (0 < i)
    putchar(C_SPC());
    putchar(C_SPC());
    i = i + -1;
  end
end

def Json_print_s(str_)
  var fd = 1; # stdout

  var i = 0;
  var c = char_at(str_, i);
  while (c != 0)
    case
    when (c == 92) # \
      write(92, fd);
      write(92, fd);
    when (c == C_LF())
      write( 92, fd); # \
      write(110, fd); # n
    when (c == C_DQ())
      write(92, fd); # \
      write(34, fd); # "
    else
      write(c, fd);
    end

    i = i + 1;
    c = char_at(str_, i);
  end
end

def _Json_print_node(node_, lv, pretty)
  var kind;
  var int_val;
  var str_;
  var list_;

  kind = Node_get_kind(node_);

  case
  when (kind == NODE_KIND__INT())
    int_val = Node_get_int(node_);
    if (pretty)
      Json_print_indent(lv);
    end
    print_i(int_val);
  when (kind == NODE_KIND__STR())
    str_ = Node_get_str(node_);
    if (pretty)
      Json_print_indent(lv);
    end
    putchar(C_DQ());
    Json_print_s(str_);
    putchar(C_DQ());
  when (kind == NODE_KIND__LIST())
    list_ = Node_get_list(node_);
    Json_print_list(list_, lv, pretty);
  else
    panic(35); # _Json_print_node: unknown node kind
  end
end

def Json_print_node(node_)
  _Json_print_node(node_, 0, true);
end

def Json_print_list(list_, lv, pretty)
  var size = List_size(list_);
  var i;
  var node_;

  if (pretty)
    Json_print_indent(lv);
  end
  putchar(91); # [
  if (pretty)
    putchar(C_LF());
  end

  i = 0;
  while (i < size)
    node_ = List_get(list_, i);
    _Json_print_node(node_, lv + 1, pretty);

    if (i < (size + -1))
      putchar(C_COMMA());
      if (__not(pretty))
        putchar(C_SPC());
      end
    end
    if (pretty)
      putchar(C_LF());
    end

    i = i + 1;
  end

  if (pretty)
    Json_print_indent(lv);
  end
  putchar(93); # ]
end

def Json_print(list_)
  Json_print_list(list_, 0, true);
  putchar(C_LF());
end

def Json_print_oneline(list_)
  Json_print_list(list_, 0, false);
end

def Json_parse_int(rest_, pos, size_)
  var pos_start;
  var pos_end;
  var n;
  var [8]s_temp;

  if (char_at(rest_, pos) == 45) # -
    pos_start = pos + 1;
  else
    pos_start = pos;
  end

  pos_end = non_digit_index(rest_, pos_start);
  substr(&s_temp, rest_, pos, pos_end);
  n = parse_int(&s_temp);

  *(size_) = __sub(pos_end, pos);
  return n;
end

def Json_parse_str(rest_, pos, s_temp_, s_temp_size, size_)
  parse_str(rest_, pos, s_temp_, s_temp_size, size_);
end

def Json_parse_list(rest_, pos_start, size_)
  var pos = pos_start;
  var size = 0;
  var list_;
  var c;
  var s_temp_size = 40; var [40]s_temp;
  var n;
  var child_list_;

  assert(
    91, # [
    char_at(rest_, pos),
    108 # Json_parse_list: first char should be [
  );
  pos = pos + 1;

  list_ = List_new();

  while (true)
    c = char_at(rest_, pos);

    case
    when (c == 93) # ]
      pos = pos + 1;
      break;

    when (c == 91) # [
      child_list_ = Json_parse_list(rest_, pos, &size);
      List_add_list(list_, child_list_);
      pos = pos + size;

    when (c == C_LF()   ) pos = pos + 1;
    when (c == C_SPC()  ) pos = pos + 1;
    when (c == C_COMMA()) pos = pos + 1;

    when (__or(
            is_digit_char(c),
            c == 45 # -
          ))
      n = Json_parse_int(rest_, pos, &size);
      List_add_int(list_, n);
      pos = pos + size;

    when (c == C_DQ())
      Json_parse_str(rest_, pos, &s_temp, s_temp_size, &size);
      List_add_str(list_, &s_temp);
      pos = pos + size;

    when (c == 0)
      p_c(c);
      panic(150); # Json_parse_list: must not happen

    else
      p_c(c);
      panic(142);
    end
  end

  *(size_) = __sub(pos, pos_start);
  return list_;
end

def Json_parse(json_)
  var list_;
  var size;

  list_ = Json_parse_list(json_, 0, &size);

  return list_;
end
