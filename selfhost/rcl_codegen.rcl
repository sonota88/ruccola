# -*- mode: ruby -*-

#include lib/std.rcl
#include lib/types.rcl
#include lib/json.rcl

# --------------------------------

def GO_STRINGS() return GO_ALLOC_CURSOR() + GS_ALLOC_CURSOR(); end
def GS_STRINGS() return 1000; end

def GO_LABEL_ID() return GO_STRINGS() + GS_STRINGS(); end
def GS_LABEL_ID() return 1; end

def GO_WHILE_CURSOR() return GO_LABEL_ID() + GS_LABEL_ID(); end
def GS_WHILE_CURSOR() return 1; end

def GO_WHILE_STACK() return GO_WHILE_CURSOR() + GS_WHILE_CURSOR(); end
def GS_WHILE_STACK() return 10; end

def GO_GVAR_NAMES() return GO_WHILE_STACK() + GS_WHILE_STACK(); end
def GS_GVAR_NAMES() return 1; end

# --------------------------------

def WhileStack_init(g_)
  *(g_ + GO_WHILE_CURSOR()) = 0;
end

def WhileStack_push(g_, label_id)
  var cur = *(g_ + GO_WHILE_CURSOR());

  *(g_ + GO_WHILE_STACK() + cur) = label_id;

  if (__le(GS_WHILE_STACK(), cur + 1))
    panic(359); # out of range
  else
    *(g_ + GO_WHILE_CURSOR()) = cur + 1;
  end
end

def WhileStack_pop(g_)
  var cur = *(g_ + GO_WHILE_CURSOR());

  if (0 < cur)
    *(g_ + GO_WHILE_CURSOR()) = cur + -1;
  else
    panic(371); # out of range
  end
end

def WhileStack_top(g_)
  var cur = *(g_ + GO_WHILE_CURSOR()) + -1;
  return *(g_ + GO_WHILE_STACK() + cur);
end

# --------------------------------

def asm_indent()
  putchar(C_SPC());
  putchar(C_SPC());
end

def asm_exit(exit_status)
  asm_indent();
  print_s("exit");
  putchar(C_SPC()); print_i(exit_status);
  putchar(C_LF());
end

def asm_label(g_, label_)
  print_s("label");
  putchar(C_SPC()); print_s(label_);
  putchar(C_LF());
end

def asm_call(g_, label_)
  asm_indent();
  print_s("call");
  putchar(C_SPC()); print_s(label_);
  putchar(C_LF());
end

def asm_ret(g_)
  asm_indent();
  print_s("ret");
  putchar(C_LF());
end

def asm_cp(g_, src_, dest_)
  asm_indent();
  print_s("cp");
  putchar(C_SPC()); print_s(src_);
  putchar(C_SPC()); print_s(dest_);
  putchar(C_LF());
end

def asm_push(g_, arg_)
  asm_indent();
  print_s("push");
  putchar(C_SPC()); print_s(arg_);
  putchar(C_LF());
end

def asm_pop(g_, dest_)
  asm_indent();
  print_s("pop");
  putchar(C_SPC()); print_s(dest_);
  putchar(C_LF());
end

def asm_sub_sp(g_, n)
  asm_indent();
  print_s("sub_sp");
  putchar(C_SPC()); print_i(n);
  putchar(C_LF());
end

def asm_add_sp(g_, n)
  asm_indent();
  print_s("add_sp");
  putchar(C_SPC()); print_i(n);
  putchar(C_LF());
end

def asm_compare(g_)
  asm_indent();
  print_s("compare");
  putchar(C_LF());
end

def asm_jump(g_, label_)
  asm_indent();
  print_s("jump");
  putchar(C_SPC()); print_s(label_);
  putchar(C_LF());
end

def asm_jump_eq(g_, label_)
  asm_indent();
  print_s("jump_eq");
  putchar(C_SPC()); print_s(label_);
  putchar(C_LF());
end

def asm_jump_g(g_, label_)
  asm_indent();
  print_s("jump_g");
  putchar(C_SPC()); print_s(label_);
  putchar(C_LF());
end

def asm_vm_comment(g_, vm_comment_)
  var vm_comment_size = 40; var [40]vm_comment;
  var i;
  var c;

  str_cp_with_check(
    &vm_comment, vm_comment_,
    vm_comment_size
  );

  i = 0;
  while (i < str_size(&vm_comment))
    c = char_at(&vm_comment, i);
    if (c == C_SPC())
      c = 126; # ~
      aset(&vm_comment, i, c);
    end
    i = i + 1;
  end

  asm_indent();
  print_s("_cmt");
  putchar(C_SPC()); print_s(&vm_comment);
  putchar(C_LF());
end

def asm_get_vram(g_, vram_addr_, dest_)
  asm_indent();
  print_s("get_vram");
  putchar(C_SPC()); print_s(vram_addr_);
  putchar(C_SPC()); print_s(dest_);
  putchar(C_LF());
end

def asm_set_vram(g_, vram_addr_, val_)
  asm_indent();
  print_s("set_vram");
  putchar(C_SPC()); print_s(vram_addr_);
  putchar(C_SPC()); print_s(val_);
  putchar(C_LF());
end

def asm_lea(g_, dest_, src_)
  asm_indent();
  print_s("lea");
  putchar(C_SPC()); print_s(dest_);
  putchar(C_SPC()); print_s(src_);
  putchar(C_LF());
end

def asm_write(g_, c, fd)
  var [8]s_c;

  unparse_int(&s_c, c);

  asm_indent();
  print_s("write");

  putchar(C_SPC()); print_s(&s_c);
  putchar(C_SPC()); print_i(fd);

  putchar(C_LF());
end

def asm_fn_prologue(g_)
  asm_push(g_, "bp");
  asm_cp(g_, "sp", "bp");
end

def asm_fn_epilogue(g_)
  asm_cp(g_, "bp", "sp");
  asm_pop(g_, "bp");
end

# --------------------------------

def render_indirection_bp(dest_, disp, dest_size)
  var [8]s_disp;

  unparse_int(&s_disp, disp);

  aset(dest_, 0,  91); # [
  aset(dest_, 1,  98); # b
  aset(dest_, 2, 112); # p
  aset(dest_, 3,  58); # :
  aset(dest_, 4,   0);

  append_str_with_check(dest_, &s_disp, dest_size);
  append_char_with_check(dest_, 93, dest_size); # ]
end

def to_fn_arg_addr(dest_, names_, name_, dest_size)
  var idx = Names_index(names_, name_);

  render_indirection_bp(dest_, idx + 2, dest_size);
end

def to_lvar_addr(dest_, names_, name_, dest_size)
  var disp = Names_disp_lvar(names_, name_);

  render_indirection_bp(dest_, disp, dest_size);
end

def append_label_id(label_, id, dest_size)
  var [8]id_str;

  append_char_with_check(label_, 95, dest_size); # _

  unparse_int(&id_str, id);
  append_str_with_check(label_, &id_str, dest_size);
end

def get_gvar_names(g_)
  return *(g_ + GO_GVAR_NAMES());
end

def is_gvar(g_, name_)
  var gvar_names_;

  gvar_names_ = get_gvar_names(g_);
  return Names_includes(gvar_names_, name_);
end

def render_indirection_gvar(dest_, base, dest_size)
  var [8]s_base;

  unparse_int(&s_base, base);

  aset(dest_, 0, 0);
  append_char_with_check(dest_, 91, dest_size); # [
  append_str_with_check(dest_, &s_base, dest_size);
  append_char_with_check(dest_, 58, dest_size); # :
  append_char_with_check(dest_, 48, dest_size); # 0
  append_char_with_check(dest_, 93, dest_size); # ]
end

def to_gvar_addr(dest_, names_, name_, dest_size)
  var base = 10;
  var offset = Names_index(names_, name_);
  var base = base + offset;

  return render_indirection_gvar(dest_, base, dest_size);
end

# --------------------------------

def _gen_expr_addr(g_, fn_arg_names_, lvar_names_, expr_)
  var list_ = Node_get_list(expr_);
  var arg_ = List_get_as_str(list_, 1);
  var disp;
  var s_src_size = 15; var [15]s_src; # [bp:-xxxxxxxx]

  if (Names_includes(lvar_names_, arg_))
    disp = Names_disp_lvar(lvar_names_, arg_);
    render_indirection_bp(&s_src, disp, s_src_size);
    asm_lea(g_, "reg_a", &s_src);
  else
    panic(526); # _gen_expr_addr
  end
end

def _gen_expr_deref(g_, fn_arg_names_, lvar_names_, expr_)
  var list_ = Node_get_list(expr_);
  var inner_expr_ = List_get(list_, 1);

  var s_reg_a_ = "reg_a";
  var s_reg_a_ind_size = 8; var [8]s_reg_a_ind; # [reg_a]

  aset(&s_reg_a_ind, 0, 0);
  append_char_with_check(&s_reg_a_ind, 91, s_reg_a_ind_size); # [
  append_str_with_check(&s_reg_a_ind, s_reg_a_, s_reg_a_ind_size);
  append_char_with_check(&s_reg_a_ind, 93, s_reg_a_ind_size); # ]

  gen_expr(g_, fn_arg_names_, lvar_names_, inner_expr_);

  asm_cp(g_, &s_reg_a_ind, s_reg_a_);
end

def _gen_expr_add(g_)
  asm_pop(g_, "reg_b");
  asm_pop(g_, "reg_a");

  asm_indent();
  print_s("add_ab");
  putchar(C_LF());
end

def _gen_expr_mult(g_)
  asm_pop(g_, "reg_b");
  asm_pop(g_, "reg_a");

  asm_indent();
  print_s("mult_ab");
  putchar(C_LF());
end

def _gen_expr_eq(g_)
  var s_reg_a_ = "reg_a";
  var label_end_size = 12; var [12]label_end; # end_eq_xxxx ... 11 chars + 1
  var label_then_size = 10; var [10]label_then; # then_xxxx ... 9 chars + 1
  var label_id;

  incr_label_id(g_);
  label_id = get_label_id(g_);

  str_cp_with_check(&label_end, "end_eq", label_end_size);
  append_label_id(&label_end, label_id, label_end_size);

  str_cp_with_check(&label_then, "then", label_then_size);
  append_label_id(&label_then, label_id, label_then_size);

  asm_pop(g_, "reg_b");
  asm_pop(g_, s_reg_a_);

  asm_compare(g_);
  asm_jump_eq(g_, &label_then);

  asm_cp(g_, "0", s_reg_a_);
  asm_jump(g_, &label_end);

  asm_label(g_, &label_then);
  asm_cp(g_, "1", s_reg_a_);

  asm_label(g_, &label_end);
end

def _gen_expr_neq(g_)
  var s_reg_a_ = "reg_a";
  var label_end_size = 13; var [13]label_end; # end_neq_xxxx ... 12 chars + 1
  var label_then_size = 10; var [10]label_then; # then_xxxx ... 9 chars + 1
  var label_id;

  incr_label_id(g_);
  label_id = get_label_id(g_);

  str_cp_with_check(&label_end, "end_neq", label_end_size);
  append_label_id(&label_end, label_id, label_end_size);

  str_cp_with_check(&label_then, "then", label_then_size);
  append_label_id(&label_then, label_id, label_then_size);

  asm_pop(g_, "reg_b");
  asm_pop(g_, s_reg_a_);

  asm_compare(g_);
  asm_jump_eq(g_, &label_then);

  asm_cp(g_, "1", s_reg_a_);
  asm_jump(g_, &label_end);

  asm_label(g_, &label_then);
  asm_cp(g_, "0", s_reg_a_);

  asm_label(g_, &label_end);
end

def _gen_expr_lt(g_)
  var s_reg_a_ = "reg_a";
  var label_end_size = 12; var [12]label_end; # end_lt_xxxx ... 11 chars + 1
  var label_then_size = 10; var [10]label_then; # then_xxxx ... 9 chars + 1
  var label_id;

  incr_label_id(g_);
  label_id = get_label_id(g_);

  str_cp_with_check(&label_end, "end_lt", label_end_size);
  append_label_id(&label_end, label_id, label_end_size);

  str_cp_with_check(&label_then, "then", label_then_size);
  append_label_id(&label_then, label_id, label_then_size);

  asm_pop(g_, "reg_b");
  asm_pop(g_, s_reg_a_);

  asm_compare(g_);
  asm_jump_g(g_, &label_then);

  asm_cp(g_, "0", s_reg_a_);
  asm_jump(g_, &label_end);

  asm_label(g_, &label_then);
  asm_cp(g_, "1", s_reg_a_);

  asm_label(g_, &label_end);
end

def _gen_expr_unary(g_, fn_arg_names_, lvar_names_, expr_)
  var list_;
  var operator_;

  list_ = Node_get_list(expr_);
  operator_ = List_get_as_str(list_, 0);

  case
  when (str_eq(operator_, "addr"))
    return _gen_expr_addr(g_, fn_arg_names_, lvar_names_, expr_);
  when (str_eq(operator_, "deref"))
    return _gen_expr_deref(g_, fn_arg_names_, lvar_names_, expr_);
  else
    p_s(operator_);
    panic(670); # _gen_expr_unary
  end
end

def _gen_expr_binary(g_, fn_arg_names_, lvar_names_, expr_)
  var expr_els_ = Node_get_list(expr_);
  var op_str_ = List_get_as_str(expr_els_, 0);
  var arg_l_ = List_get(expr_els_, 1);
  var arg_r_ = List_get(expr_els_, 2);
  var s_reg_a_ = "reg_a";

  gen_expr(g_, fn_arg_names_, lvar_names_, arg_l_);
  asm_push(g_, s_reg_a_);

  gen_expr(g_, fn_arg_names_, lvar_names_, arg_r_);
  asm_push(g_, s_reg_a_);

  case
  when (char_at(op_str_, 0) == 43) # +
    _gen_expr_add(g_);
  when (char_at(op_str_, 0) == 42) # *
    _gen_expr_mult(g_);
  when (str_eq(op_str_, "=="))
    _gen_expr_eq(g_);
  when (str_eq(op_str_, "!="))
    _gen_expr_neq(g_);
  when (char_at(op_str_, 0) == 60) # <
    _gen_expr_lt(g_);
  else
    panic(606); # _gen_expr_binary: unsupported
  end
end

def gen_expr(g_, fn_arg_names_, lvar_names_, expr_)
  var cp_src_size = 20; var [20]cp_src;
  var s_reg_a_ = "reg_a";
  var expr;
  var str_;
  var list_;
  var gvar_names_;

  case
  when (Node_kind_eq(expr_, NODE_KIND__INT()))
    expr = Node_get_int(expr_);
    unparse_int(&cp_src, expr);
    asm_cp(g_, &cp_src, s_reg_a_);

  when (Node_kind_eq(expr_, NODE_KIND__STR()))
    str_ = Node_get_str(expr_);

    case
    when (str_eq(str_, "true"))
      aset(&cp_src, 0, 49); # 1
      aset(&cp_src, 1,  0);
      asm_cp(g_, &cp_src, s_reg_a_);

    when (str_eq(str_, "false"))
      aset(&cp_src, 0, 48); # 0
      aset(&cp_src, 1,  0);
      asm_cp(g_, &cp_src, s_reg_a_);

    when (Names_includes(fn_arg_names_, str_))
      to_fn_arg_addr(&cp_src, fn_arg_names_, str_, cp_src_size);
      asm_cp(g_, &cp_src, s_reg_a_);

    when (Names_includes(lvar_names_, str_))
      to_lvar_addr(&cp_src, lvar_names_, str_, cp_src_size);
      asm_cp(g_, &cp_src, s_reg_a_);

    when (is_gvar(g_, str_))
      gvar_names_ = get_gvar_names(g_);
      to_gvar_addr(&cp_src, gvar_names_, str_, cp_src_size);
      asm_cp(g_, &cp_src, s_reg_a_);

    else
      Json_print(Names_to_plain(g_, fn_arg_names_));
      Json_print(Names_to_plain(g_, lvar_names_));
      Json_print_node(expr_);
      panic(681); # gen_expr: no such function argument or local variable
    end

  when (Node_kind_eq(expr_, NODE_KIND__LIST()))
    list_ = Node_get_list(expr_);

    if (str_eq(
          List_get_as_str(list_, 0),
          "funcall"
        ))
      gen_call(g_, fn_arg_names_, lvar_names_, list_);
    else
      case
      when (List_size(list_) == 2)
        _gen_expr_unary(g_, fn_arg_names_, lvar_names_, expr_);
      when (List_size(list_) == 3)
        _gen_expr_binary(g_, fn_arg_names_, lvar_names_, expr_);
      else
        panic(742); # gen_expr
      end
    end

  else
    Json_print_node(expr_);
    panic(344); # gen_expr: not yet implemented
  end
end

def gen_call(g_, fn_arg_names_, lvar_names_, stmt_)
  var vm_comment_size = 32; var [32]vm_comment;
  var fn_name_;
  var args_;
  var i;
  var node_;
  var s_reg_a_ = "reg_a";

  fn_name_ = List_get_as_str(stmt_, 1);
  args_ = List_rest(g_, stmt_, 2);

  str_cp_with_check(&vm_comment, "call", vm_comment_size);
  aset(&vm_comment, 4, 58); # :
  aset(&vm_comment, 5, 0);
  append_str_with_check(
    &vm_comment, fn_name_,
    vm_comment_size
  );

  i = __sub(List_size(args_), 1);
  while (__le(0, i))
    node_ = List_get(args_, i);
    gen_expr(g_, fn_arg_names_, lvar_names_, node_);
    asm_push(g_, s_reg_a_);
    i = __sub(i, 1);
  end

  asm_vm_comment(g_, &vm_comment);
  asm_call(g_, fn_name_);

  if (0 < List_size(args_))
    asm_add_sp(g_, List_size(args_));
  end
end

def gen_set(g_, fn_arg_names_, lvar_names_, stmt_)
  var dest_size = 15; var [15]dest; # [bp:-xxxxxxxx] 14 chars + 1
  var dest_node_;
  var [8]dest_str_;
  var s_reg_a_ = "reg_a";
  var s_reg_b_ = "reg_b";
  var expr_node_;
  var list_;
  var s_reg_a_ind_size = 8; var [8]s_reg_a_ind; # [reg_a]
  var gvar_names_;

  # "[reg_a]"
  aset(&s_reg_a_ind, 0, 0);
  append_char_with_check(&s_reg_a_ind, 91, s_reg_a_ind_size); # [
  append_str_with_check(&s_reg_a_ind, s_reg_a_, s_reg_a_ind_size);
  append_char_with_check(&s_reg_a_ind, 93, s_reg_a_ind_size); # ]

  dest_node_ = List_get(stmt_, 1);
  expr_node_ = List_get(stmt_, 2);

  gen_expr(g_, fn_arg_names_, lvar_names_, expr_node_);

  case
  when (Node_get_kind(dest_node_) == NODE_KIND__STR())
    dest_str_ = Node_get_str(dest_node_);

    case
    when (Names_includes(lvar_names_, dest_str_))
      to_lvar_addr(&dest, lvar_names_, dest_str_, dest_size);
      asm_cp(g_, s_reg_a_, &dest);
    when (is_gvar(g_, dest_str_))
      gvar_names_ = get_gvar_names(g_);
      to_gvar_addr(&dest, gvar_names_, dest_str_, dest_size);
      asm_cp(g_, s_reg_a_, &dest);
    else
      Json_print(stmt_);
      p_s(&dest);
      panic(823); # gen_set
    end

  when (Node_get_kind(dest_node_) == NODE_KIND__LIST())
    list_ = Node_get_list(dest_node_);

    if (str_eq(List_get_as_str(list_, 0), "deref"))
      asm_push(g_, s_reg_a_);

      gen_expr(g_, fn_arg_names_, lvar_names_, List_get(list_, 1));

      asm_pop(g_, s_reg_b_);
      asm_cp(g_, s_reg_b_, &s_reg_a_ind);
    else
      panic(859); # gen_set
    end

  else
    panic(853); # gen_set
  end
end

def gen_return(g_, fn_arg_names_, lvar_names_, stmt_)
  var expr_;

  if (List_size(stmt_) == 2)
    expr_ = List_get(stmt_, 1);
    gen_expr(g_, fn_arg_names_, lvar_names_, expr_);
  end

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

def gen_while(g_, fn_arg_names_, lvar_names_, stmt_)
  var label_begin_size = 11; var [11]label_begin; # while_xxxx
  var label_end_size = 15; var [15]label_end; # end_while_xxxx

  var label_id;
  var cond_expr_node_ = List_get(stmt_, 1);
  var stmts_ = List_get_as_list(stmt_, 2);

  incr_label_id(g_);
  label_id = get_label_id(g_);
  WhileStack_push(g_, label_id);

  str_cp_with_check(&label_begin, "while", label_begin_size);
  append_label_id(&label_begin, label_id, label_begin_size);

  str_cp_with_check(&label_end, "end_while", label_end_size);
  append_label_id(&label_end, label_id, label_end_size);

  asm_label(g_, &label_begin);

  gen_expr(g_, fn_arg_names_, lvar_names_, cond_expr_node_);

  asm_cp(g_, "0", "reg_b");
  asm_compare(g_);

  asm_jump_eq(g_, &label_end);

  gen_stmts(g_, fn_arg_names_, lvar_names_, stmts_);
  asm_jump(g_, &label_begin);

  asm_label(g_, &label_end);

  WhileStack_pop(g_);
end

def gen_break(g_)
  var label_id = WhileStack_top(g_);

  var label_end_size = 15; var [15]label_end; # end_while_xxxx
  str_cp_with_check(&label_end, "end_while", label_end_size);
  append_label_id(&label_end, label_id, label_end_size);

  asm_jump(g_, &label_end);

  putchar(C_LF());
end

def gen_case(g_, fn_arg_names_, lvar_names_, stmt_)
  var i;
  var when_clauses_;
  var when_clause_;
  var cond_expr_node_;
  var stmts_;
  var label_id;

  var label_size = 19; # end_when_xxxx_xxxx
  var [19]label_end;
  var [19]label_end_when_head;
  var [19]label_temp;

  var s_0_ = "0";
  var s_reg_b_ = "reg_b";
  var when_index = -1;

  incr_label_id(g_);
  label_id = get_label_id(g_);

  str_cp_with_check(&label_end, "end_case", label_size);
  append_label_id(&label_end, label_id, label_size);

  str_cp_with_check(&label_end_when_head, "end_when", label_size);
  append_label_id(&label_end_when_head, label_id, label_size);

  when_clauses_ = List_rest(g_, stmt_, 1);

  i = 0;
  while (i < List_size(when_clauses_))
    when_index = when_index + 1;
    when_clause_ = List_get_as_list(when_clauses_, i);
    cond_expr_node_ = List_get(when_clause_, 0);
    stmts_ = List_rest(g_, when_clause_, 1);

    gen_expr(g_, fn_arg_names_, lvar_names_, cond_expr_node_);
    asm_cp(g_, s_0_, s_reg_b_);
    asm_compare(g_);

    # {label_end_when_head}_{when_index}
    str_cp(&label_temp, &label_end_when_head);
    append_label_id(&label_temp, when_index, label_size);
    asm_jump_eq(g_, &label_temp);

    gen_stmts(g_, fn_arg_names_, lvar_names_, stmts_);

    asm_jump(g_, &label_end);

    # {label_end_when_head}_{when_index}
    str_cp(&label_temp, &label_end_when_head);
    append_label_id(&label_temp, when_index, label_size);
    asm_label(g_, &label_temp);

    i = i + 1;
  end

  asm_label(g_, &label_end);
end

def gen_vm_comment(g_, stmt_)
  asm_vm_comment(g_, List_get_as_str(stmt_, 1));
end

def gen_stmt(g_, fn_arg_names_, lvar_names_, stmt_)
  var head_str_ = List_get_as_str(stmt_, 0);

  case
  when (str_eq(head_str_, "set"))
    gen_set(g_, fn_arg_names_, lvar_names_, stmt_);
  when (str_eq(head_str_, "_cmt"))
    gen_vm_comment(g_, stmt_);
  when (str_eq(head_str_, "call"))
    gen_call(g_, fn_arg_names_, lvar_names_, stmt_);
  when (str_eq(head_str_, "return"))
    gen_return(g_, fn_arg_names_, lvar_names_, stmt_);
  when (str_eq(head_str_, "while"))
    gen_while(g_, fn_arg_names_, lvar_names_, stmt_);
  when (str_eq(head_str_, "break"))
    gen_break(g_);
  when (str_eq(head_str_, "case"))
    gen_case(g_, fn_arg_names_, lvar_names_, stmt_);
  else
    panic(365); # gen_stmt
  end
end

def gen_stmts(g_, fn_arg_names_, lvar_names_, stmts_)
  var i = 0;
  var stmt_;

  while (i < List_size(stmts_))
    stmt_ = List_get_as_list(stmts_, i);
    gen_stmt(g_, fn_arg_names_, lvar_names_, stmt_);
    i = i + 1;
  end
end

def gen_var(g_, fn_arg_names_, lvar_names_, stmt_)
  var set_stmt_;

  asm_sub_sp(g_, 1);

  if (List_size(stmt_) == 3)
    set_stmt_ = List_new(g_);
    List_add_str(g_, set_stmt_, "set");
    List_add(g_, set_stmt_, List_get(stmt_, 1));
    List_add(g_, set_stmt_, List_get(stmt_, 2));

    gen_set(g_, fn_arg_names_, lvar_names_, set_stmt_);
  end
end

def gen_var_array(g_, fn_arg_names_, lvar_names_, stmt_)
  var width = List_get_as_int(stmt_, 2);

  asm_sub_sp(g_, width);
end

def gen_func(g_, func_)
  var fn_name_;
  var stmts_;
  var stmt_;
  var i;
  var lvar_names_;
  var lvar_name_;
  var lvar_width_;
  var fn_arg_names_;
  var s_var_ = "var";
  var s_var_array_ = "var_array";
  var stmt_head_;

  fn_name_ = Node_get_str(List_get(func_, 1));

  fn_arg_names_ =
    Names_from_node_list(
      g_,
      List_get_as_list(func_, 2)
    );

  stmts_ = Node_get_list(List_get(func_, 3));

  asm_label(g_, fn_name_);

  asm_fn_prologue(g_);

  lvar_names_ = Names_new(g_);

  i = 0;
  while (i < List_size(stmts_))
    stmt_ = List_get_as_list(stmts_, i);
    stmt_head_ = List_get_as_str(stmt_, 0);

    case
    when (str_eq(stmt_head_, s_var_))
      lvar_name_ = List_get_as_str(stmt_, 1);
      Names_add(g_, lvar_names_, lvar_name_, 1);
      gen_var(g_, fn_arg_names_, lvar_names_, stmt_);
    when (str_eq(stmt_head_, s_var_array_))
      lvar_name_ = List_get_as_str(stmt_, 1);
      lvar_width_ = List_get_as_int(stmt_, 2);
      Names_add(g_, lvar_names_, lvar_name_, lvar_width_);
      gen_var_array(g_, fn_arg_names_, lvar_names_, stmt_);
    else
      gen_stmt(g_, fn_arg_names_, lvar_names_, stmt_);
    end

    i = i + 1;
  end

  asm_fn_epilogue(g_);

  asm_ret(g_);
end

# (gvar {name})
def register_gvar(g_, top_stmt_)
  var gvar_names_;
  var gvar_name_;

  gvar_name_ = List_get_as_str(top_stmt_, 1);

  gvar_names_ = get_gvar_names(g_);
  Names_add(g_, gvar_names_, gvar_name_, 1);
end

def gen_top_stmt(g_, top_stmt_)
  var s_head_ = List_get_as_str(top_stmt_, 0);

  case
  when (str_eq(s_head_, "func"))
    gen_func(g_, top_stmt_);
  when (str_eq(s_head_, "gvar"))
    register_gvar(g_, top_stmt_);
  else
    panic(194); # gen_top_stmt
  end
end

def gen_top_stmts(g_, top_stmts_)
  var i;
  var top_stmt_;

  i = 1;
  while (i < List_size(top_stmts_))
    top_stmt_ = List_get_as_list(top_stmts_, i);
    gen_top_stmt(g_, top_stmt_);
    i = i + 1;
  end
end

# --------------------------------

def gen_builtin_write(g_)
  var s_reg_a_ = "reg_a";
  var s_write_ = "write";
  var s_fn_arg1_size = 7; var [7]s_fn_arg1;
  var s_fn_arg2_size = 7; var [7]s_fn_arg2;

  render_indirection_bp(&s_fn_arg1, 2, s_fn_arg1_size);
  render_indirection_bp(&s_fn_arg2, 3, s_fn_arg2_size);

  asm_label(g_, s_write_);
  asm_fn_prologue(g_);

  asm_cp(g_, &s_fn_arg1, s_reg_a_);

  asm_indent();
  print_s(s_write_);
  putchar(C_SPC()); print_s(s_reg_a_);
  putchar(C_SPC()); print_s(&s_fn_arg2);
  putchar(C_LF());

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

def gen_builtin_getchar(g_)
  var s_fn_arg1_size = 7; var [7]s_fn_arg1;

  render_indirection_bp(&s_fn_arg1, 2, s_fn_arg1_size);

  asm_label(g_, "getchar");
  asm_fn_prologue(g_);

  asm_indent();
  print_s("read");
  putchar(C_SPC()); print_s("reg_a");
  putchar(C_LF());

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

def gen_builtin_get_sp(g_)
  asm_label(g_, "get_sp");

  asm_fn_prologue(g_);

  asm_cp(g_, "sp", "reg_a");

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

def gen_builtin_panic(g_)
  asm_label(g_, "_panic");

  asm_write(g_, 80, 2); # P
  asm_write(g_, 65, 2); # A
  asm_write(g_, 78, 2); # N
  asm_write(g_, 73, 2); # I
  asm_write(g_, 67, 2); # C
  asm_write(g_, C_LF(), 2);

  asm_exit(1);
end

def gen_builtin_set_vram(g_)
  var s_vram_addr_size = 7; var [7]s_vram_addr;
  var s_val_size = 7; var [7]s_val;

  render_indirection_bp(&s_vram_addr, 2, s_vram_addr_size);
  render_indirection_bp(&s_val, 3, s_val_size);

  asm_label(g_, "set_vram");
  asm_fn_prologue(g_);

  asm_set_vram(g_, &s_vram_addr, &s_val);

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

def gen_builtin_get_vram(g_)
  var s_vram_addr_size = 7; var [7]s_vram_addr;

  render_indirection_bp(&s_vram_addr, 2, s_vram_addr_size);

  asm_label(g_, "get_vram");
  asm_fn_prologue(g_);

  asm_get_vram(g_, &s_vram_addr, "reg_a");

  asm_fn_epilogue(g_);
  asm_ret(g_);
end

# --------------------------------

def codegen(g_, ast_)
  asm_call(g_, "main");
  asm_exit(0);

  gen_top_stmts(g_, ast_);

  print_s("#>builtins");
  putchar(C_LF());

  gen_builtin_write(g_);
  gen_builtin_getchar(g_);
  gen_builtin_get_sp(g_);
  gen_builtin_panic(g_);
  gen_builtin_set_vram(g_);
  gen_builtin_get_vram(g_);

  print_s("#<builtins");
  putchar(C_LF());
end

def get_label_id(g_)
  return *(g_ + GO_LABEL_ID());
end

def incr_label_id(g_)
  *(g_ + GO_LABEL_ID()) = get_label_id(g_) + 1;
end

def init_gvar_names(g_)
  *(g_ + GO_GVAR_NAMES()) = Names_new(g_);
end

def load_ast(g_)
  var input_size_max = 260000; var [260000]input;

  read_stdin_all(&input, input_size_max);
  return Json_parse(g_, &input);
end

def _main(g_)
  var ast_;

  init_gvar_names(g_);
  WhileStack_init(g_);

  ast_ = load_ast(g_);
  codegen(g_, ast_);
end

def main()
  var [1015]g;
  var g_ = &g;

  init_globals(g_);
  init_strings();

  _main(g_);
end
